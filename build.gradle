plugins {
    id 'scala'
    id 'war'
    id 'jetty'
    id "com.github.maiflai.scalatest" version "0.14"
}

task dist(type: Jar) {
    manifest {
        attributes 'Main-Class': 'uk.sky.tech.rafiki.Main'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

ext {
    scalaVersion = '2.12'
    scalaMinor = '1'
    liftVersion = '3.1.1'
    specs2Version = "1.14"
    seleniumVersion = "3.0.1"
}

repositories {
    mavenCentral()
    mavenLocal()
    maven{
        name = 'Artifactory'
        url = 'http://repo.sns.sky.com:8081/artifactory/libs-releases'
    }
    maven{
        name = 'Artifactory plugins'
        url = 'http://repo.sns.sky.com:8081/artifactory/plugins-releases'
    }
}

sourceSets {
    acceptanceTest {
        scala.srcDir file('src/acceptance/scala')
        resources.srcDir file('src/acceptance/resources')
        compileClasspath += main.output + test.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }

    test {
        resources.srcDir file('src/main/webapp/')
    }
}

task acceptanceTest(type: Test) {
    description = 'Runs the acceptance tests'
    group = 'verification'
    testClassesDir = sourceSets.acceptanceTest.output.classesDir
    classpath = sourceSets.acceptanceTest.runtimeClasspath
    reports.junitXml.destination = "$buildDir/acceptance-test-results"
    reports.html.destination = "$buildDir/reports/acceptanceTest"
    dependsOn(test)
}

check {
    dependsOn(acceptanceTest)
}

jettyRun {
    contextPath = "/"
    reload = "automatic"
    scanIntervalSeconds = 5
}

configurations {
    scalaCompiler
    scalaLibrary
}

dependencies {
    scalaCompiler "org.scala-lang:scala-compiler:$scalaVersion.$scalaMinor"
    scalaLibrary "org.scala-lang:scala-library:$scalaVersion.$scalaMinor"

    compile "org.scala-lang:scala-library:$scalaVersion.$scalaMinor",
            "net.liftweb:lift-mapper_$scalaVersion:$liftVersion",
            "net.liftweb:lift-webkit_$scalaVersion:$liftVersion",
            "net.liftweb:lift-util_$scalaVersion:$liftVersion",
            "net.liftweb:lift-actor_$scalaVersion:$liftVersion",
            "net.liftweb:lift-common_$scalaVersion:$liftVersion",
            "net.liftweb:lift-json_$scalaVersion:$liftVersion",
            'org.mortbay.jetty:jetty:6.1.25',
            'org.mortbay.jetty:jetty-util:6.1.25',
            'org.mortbay.jetty:jetty-management:6.1.25',
            "ch.qos.logback:logback-classic:1.0.13",
            'io.fabric8:kubernetes-client:3.1.8',
            'sky.sns.hermes:hermes-status-service:14'

    testCompile "junit:junit:4.12",
            "org.scalatest:scalatest_$scalaVersion:3.0.1",
            "net.liftweb:lift-testkit_$scalaVersion:$liftVersion",
            'org.mockito:mockito-core:2.2.15',
            'com.flextrade.jfixture:jfixture:2.6.3',
            'com.flextrade.jfixture:jfixture-scala:0.0.1',
            "org.seleniumhq.selenium:selenium-api:$seleniumVersion",
            "org.seleniumhq.selenium:selenium-java:$seleniumVersion",
            "org.seleniumhq.selenium:selenium-chrome-driver:$seleniumVersion",
            "org.seleniumhq.selenium:selenium-firefox-driver:$seleniumVersion"

    testRuntime 'org.pegdown:pegdown:1.6.0'

    providedCompile 'javax.servlet:servlet-api:2.5'
}
